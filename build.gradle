/*
 * build.gradle
 *
 * Copyright by toolarium, all rights reserved.
 */
plugins {
  id 'com.gradleup.shadow' version '8.3.5'
  //id 'com.github.johnrengelman.shadow' version '8.1.1' --> changed because of java 21
}

apply from: "https://raw.githubusercontent.com/toolarium/common-gradle-build/master/gradle/common.gradle"


/***************************************************************************************
 * define the main class name
 ***************************************************************************************/
mainClassName = "com.github.toolarium.jwebserver.JWebServer"

// Minimizing an shadow JAR
def excludedDependenciesGroups = [ //'com.google.code.findbugs'
]
def excludedDependenciesArtifacts = [ //'logback-classic', 'jboss-logging'
]

shadowJar {
  minimize {
      exclude(dependency('org.fusesource.jansi:.*:.*'))
      exclude(dependency('io.undertow:.*:.*'))
      exclude(dependency('ch.qos.logback:.*:.*'))
      exclude(dependency('org.bouncycastle:.*:.*'))
  }

  dependencies {
      exclude(dependency {
            if (excludedDependenciesGroups.contains(it.moduleGroup) || excludedDependenciesArtifacts.contains(it.moduleName)) {
                logInfo("> Exclude dependency ["+it.moduleName+"]/["+it.moduleGroup + "]")
                return true;
            }
            logInfo("> Keep dependency ["+it.moduleName+"]/["+it.moduleGroup + "]")
            return false
       })
    }
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}


/****************************************************************************************
 * Define project dependencies
 ****************************************************************************************/
dependencies {

    // undertow web server
    implementation 'io.undertow:undertow-core:2.3.18.Final'

    // certificate utils    
    implementation "com.github.toolarium:toolarium-common:0.9.1"
    implementation "com.github.toolarium:toolarium-security:1.1.4"
    
    // security library
    implementation 'org.bouncycastle:bcprov-jdk18on:1.79'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.79'
    
    // CLI
    // https://mvnrepository.com/artifact/org.fusesource.jansi/jansi
    implementation 'org.fusesource.jansi:jansi:2.4.1'
    implementation 'info.picocli:picocli:4.7.6'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.6'

    // logging
    implementation "org.slf4j:slf4j-api:${commonGradleSlf4jApiVersion}"
    implementation "ch.qos.logback:logback-classic:${commonGradleLogbackVersion}"

    // test
    testImplementation 'io.rest-assured:rest-assured:5.5.0'
}
